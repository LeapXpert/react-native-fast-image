buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.4.0"
    }
}

apply plugin: "com.android.library"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _excludeAppGlideModule = safeExtGet("excludeAppGlideModule", false)
def _buildToolsVersion = safeExtGet("buildToolsVersion", "28.0.3")
def _compileSdkVersion = safeExtGet("compileSdkVersion", 28)
def _glideVersion = safeExtGet("glideVersion", "4.11.0")
def _minSdkVersion = safeExtGet("minSdkVersion", 16)
def _reactNativeVersion = safeExtGet("reactNative", "+")
def _targetSdkVersion = safeExtGet("targetSdkVersion", 28)
def _blackberryVersion = safeExtGet("blackberry_version", "8.1.0.37")

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    sourceSets {
        main {
            java {
                if (_excludeAppGlideModule) {
                    srcDir "src"
                    exclude "**/FastImageGlideModule.java"
                }
            }
        }
    }

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:${_reactNativeVersion}"
    implementation "androidx.core:core:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.annotation:annotation:1.0.0"
    implementation("com.github.bumptech.glide:glide:${_glideVersion}")
    implementation("com.github.bumptech.glide:annotations:${_glideVersion}")
    annotationProcessor 'androidx.annotation:annotation:1.0.0'
    implementation project(':blackberry-utils')
    annotationProcessor "com.github.bumptech.glide:compiler:${_glideVersion}"
    implementation("com.github.bumptech.glide:okhttp3-integration:${_glideVersion}")
    implementation "com.blackberry.blackberrydynamics:android_handheld_platform:${_blackberryVersion}"
    implementation "com.blackberry.blackberrydynamics:android_handheld_backup_support:${_blackberryVersion}"
}
